<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="Form.REST.File">
<Description>
This broker does file-related work.
All file properies are assumed to be of Form.File class</Description>
<Super>Form.REST.Abstract</Super>
<TimeCreated>64091,74902.525566</TimeCreated>

<XData name="UrlMap">
<XMLNamespace>http://www.intersystems.com/urlmap</XMLNamespace>
<Data><![CDATA[
<Routes>
<Route Url="/:class/:id/:property" Method="POST" Call="addFiles"/>
<Route Url="/:class/:id/:property" Method="DELETE" Call="deleteFiles"/>
<Route Url="/:class/:id/:property/:filename" Method="DELETE" Call="deleteFile"/>
<Route Url="/:class/:id/:property/:filename" Method="GET" Call="serveFile"/>
</Routes>
]]></Data>
</XData>

<Method name="addFiles">
<Description>
class and id are object identificators.  
If it's not a list property, then the first file is set as property value </Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>class:%String="",id:%Integer="",property:%String=""</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#dim sc As %Status = $$$OK
	set sc = ##class(Form.Info).formExistsStatus(class)
	return:$$$ISERR(sc) sc
	set sc = ..checkObjPermission(class, "U")
	return:$$$ISERR(sc) sc
	
	set obj = $classMethod(class, "%OpenId", id, , .sc)
	return:$$$ISERR(sc) sc
	
	set propertyType = $$$comMemberKeyGet(class, $$$cCLASSproperty, property, $$$cPROPtype)
	return:propertyType'=$$$fileClass $$$ERROR($$$GeneralError, property _ " is not file property")

	set propertyCollectionType = $$$comMemberKeyGet(class ,$$$cCLASSproperty, property, $$$cPROPcollection)
	
	
	#dim stream As %CSP.BinaryStream
	set filename = ""
	if propertyCollectionType = "" {
		set filename = %request.NextMimeData(filename)
		quit:filename=""
		set stream = %request.GetMimeData(filename) // Проверить на больших файлах/делит ли ангулар
		set attachment = ##class(Form.File).%New(stream.FileName, filename, stream)
		set $property(obj, property) = attachment  
	} else {
		set propertyOREF = $property(obj, property)
		for {
	        set filename = %request.NextMimeData(filename)
	        quit:filename=""
	        
	        set stream = %request.GetMimeData(filename) // Проверить на больших файлах/делит ли ангулар
	        set attachment = ##class(Form.File).%New(stream.FileName, ..getFileDir(class, id, property), filename, stream)
	        if propertyCollectionType="list" {
		        // propertyOREF тут %Collection.ListOfObj
	        	do $method(propertyOREF, "Insert", attachment)
	        } else {
		        /// Тут если массивы. 
		        /// Как получить следующий индекс
		        /// Так как у нас пока нет, то не пишу		        
	        }
	    }
	}
	set sc = obj.%Save()
	
	return sc
]]></Implementation>
</Method>

<Method name="deleteFiles">
<Description>
Clear property value for object with class/id
do ##class(Form.REST.File).deleteFiles("Form.Test.Simple", 1, "file")</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>class:%String="",id:%Integer="",property:%String=""</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#dim sc As %Status = $$$OK
	set sc = ##class(Form.Info).formExistsStatus(class)
	return:$$$ISERR(sc) sc
	set sc = ..checkObjPermission(class, "U")
	return:$$$ISERR(sc) sc
	
	set obj = $classMethod(class, "%OpenId", id, , .sc)
	return:$$$ISERR(sc) sc
	
	set propertyType = $$$comMemberKeyGet(class, $$$cCLASSproperty, property, $$$cPROPtype)
	
	return:propertyType'=$$$fileClass $$$ERROR($$$GeneralError, property _ " is not file property")
	
	set $property(obj, property) = ""
	set sc = obj.%Save()
	
	return sc
]]></Implementation>
</Method>

<Method name="deleteFile">
<Description>
Delete value with name=filename from property for object with class/id</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>class:%String="",id:%Integer="",property:%String="",filename=""</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#dim sc As %Status = $$$OK
	set sc = ##class(Form.Info).formExistsStatus(class)
	return:$$$ISERR(sc) sc
	set sc = ..checkObjPermission(class, "U")
	return:$$$ISERR(sc) sc
	
	set obj = $classMethod(class, "%OpenId", id, , .sc)
	return:$$$ISERR(sc) sc
	
	set propertyType = $$$comMemberKeyGet(class, $$$cCLASSproperty, property, $$$cPROPtype)
	
	return:propertyType'=$$$fileClass $$$ERROR($$$GeneralError, property _ " is not file property")
	
	set propertyCollectionType = $$$comMemberKeyGet(class ,$$$cCLASSproperty, property, $$$cPROPcollection)
	
	
	#dim stream As %CSP.BinaryStream
	if propertyCollectionType = "" {
		set propertyOREF = $property(obj, property)
		set storedFilename = propertyOREF.name
		if (storedFilename = filename) {
			set fullFilename = propertyOREF.realName
			do ##class(%File).Delete(fullFilename)
			set $property(obj, property) = ""
		}  else {
			set sc = $$$ERROR($$$GeneralError, filename _ " is incorrect")
		}
	} else {
		#dim propertyOREF As %Collection.ListOfObj
		set propertyOREF = $property(obj, property)
		for i=1:1:propertyOREF.Count() {
			set file = propertyOREF.GetAt(i)
			set storedFilename = file.name
			if (storedFilename=filename) {
				set fullFilename = propertyOREF.realName
				do ##class(%File).Delete(fullFilename)
				do propertyOREF.RemoveAt(i)
				/// Delete only first file
				quit
			}
		}
	}
	
	return:$$$ISERR(sc) sc
	set sc = obj.%Save()
	
	
		
	return sc
]]></Implementation>
</Method>

<Method name="serveFile">
<Description>
Serve file with name=filename from property for object with class/id</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>class:%String="",id:%Integer="",property:%String="",filename=""</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#dim sc As %Status = $$$OK
	set sc = ##class(Form.Info).formExistsStatus(class)
	return:$$$ISERR(sc) sc
	set sc = ..checkObjPermission(class, "R")
	return:$$$ISERR(sc) sc
	
	set class = ##class(Form.Info).getFormClass(class)
	set obj = $classMethod(class, "%OpenId", id, , .sc)
	return:$$$ISERR(sc) sc
	
	set propertyType = $$$comMemberKeyGet(class, $$$cCLASSproperty, property, $$$cPROPtype)
	
	return:propertyType'=$$$fileClass $$$ERROR($$$GeneralError, property _ " is not file property")
	
	set propertyCollectionType = $$$comMemberKeyGet(class ,$$$cCLASSproperty, property, $$$cPROPcollection)
	
	
	#dim stream As %CSP.BinaryStream
	if propertyCollectionType = "" {
		set propertyOREF = $property(obj, property)
		set realFilename = $property(propertyOREF, "name")
		if (realFilename = filename) {
			#dim file As Form.File
			set file = $property(obj, property)
			set sc = file.serve()
		}  else {
			set sc = $$$ERROR($$$GeneralError, filename _ " is incorrect, right one is " _ realFilename)
		}
	} else {
		#dim propertyOREF As %Collection.ListOfObj
		set propertyOREF = $property(obj, property)
		for i=1:1:propertyOREF.Count() {
			set file = propertyOREF.GetAt(i)
			set realFilename = file.name
			if (realFilename=filename) {
				set sc = file.serve()
				quit
			}
		}
	}
	
	return sc
]]></Implementation>
</Method>

<Method name="getFileDir">
<Description>
Get directory path based on structure: basedir/class/id/property </Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>class:%String,id:%String,property:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	set directory = ##class(%File).NormalizeDirectory($$$fileDir)
	set directory = ##class(%File).NormalizeDirectory(directory _ class)
	set directory = ##class(%File).NormalizeDirectory(directory _ id)
	set directory = ##class(%File).NormalizeDirectory(directory _ property)
	if '##class(%File).DirectoryExists(directory) {
		do ##class(%File).CreateDirectoryChain(directory)
	}
	quit directory
]]></Implementation>
</Method>

<Method name="filenameToNameExt">
<Description>
Get file extension ext and filename (without extension) name from filename (without path)</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>filename:%String,*name:%String,*ext:%String</FormalSpec>
<Implementation><![CDATA[
	set ext = "", name = ""
	if $length(filename, ".") > 1 {
		set name = $piece(filename, ".", 1, *-1)
		set ext = $piece(filename, ".", *)
	} else {
		set name = filename
		set ext = ""
	}
]]></Implementation>
</Method>
</Class>
</Export>

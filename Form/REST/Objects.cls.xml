<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="Form.REST.Objects">
<Description>
Broker class to work with queries</Description>
<Super>Form.REST.Abstract</Super>
<TimeCreated>64090,79332.572705</TimeCreated>

<Parameter name="ALL">
<Description>
Query to return all availible information about form objects</Description>
<Default>*, %CLASSNAME AS _class</Default>
</Parameter>

<XData name="UrlMap">
<XMLNamespace>http://www.intersystems.com/urlmap</XMLNamespace>
<Data><![CDATA[
<Routes>
<Route Url="/:form/custom/:query" Method="GET" Call="getFormObjectsCustom"/>
<Route Url="/:form/:query" Method="GET" Call="getFormObjects"/>
</Routes>
]]></Data>
</XData>

<Method name="getFormObjects">
<Description>
Get all form objects</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>class:%String="",queryType:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#dim sc As %Status = $$$OK
	set sc = ##class(Form.Info).formExistsStatus(class)
	return:$$$ISERR(sc) sc
	set sc = ..checkObjPermission(class, "R")
	return:$$$ISERR(sc) sc
	set sc = ..getBaseQuery(queryType, class, .queryBase)
	return:$$$ISERR(sc) sc	
	
	set table = $$$ClassSQLTable(class)
	set sql = queryBase _ table
	
	set sc = ##class(Form.JSON.SQL).WriteJSONFromSQL(sql, class)
	return sc
]]></Implementation>
</Method>

<Method name="getBaseQuery">
<Description><![CDATA[
Returns the part between SELECT and FORM (queryBase) based on queryType and target class
Supported queryBase sources:<br>
1. Uppercase parameter values defined in this class<br>
2. ClassMethods, defined in this class with the name: queryQUERYTYPE]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>queryType:%String,class:%String,*queryBase:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set queryBase = ""
	set queryType = $ZCVT(queryType, "U")
	set userQueryClass = ##class(Form.Settings).getSetting("queryclass")
	#dim sc As %Status = $$$OK
	if ##class(%Dictionary.MethodDefinition).IDKEYExists(class, "query" _ queryType) {
		set queryBase = $classmethod(class, "query" _ queryType)
	} elseif ((userQueryClass'="") && (##class(%Dictionary.ParameterDefinition).IDKEYExists(userQueryClass, queryType))) {
		set queryBase = $parameter(userQueryClass, queryType)
	} elseif ((userQueryClass'="") && (##class(%Dictionary.MethodDefinition).IDKEYExists(userQueryClass, "query" _ queryType))) {
		set queryBase = $classmethod(userQueryClass, "query" _ queryType, class)
	} elseif ##class(%Dictionary.ParameterDefinition).IDKEYExists($classname(), queryType) {
		set queryBase = $parameter(, queryType)
	} elseif ##class(%Dictionary.MethodDefinition).IDKEYExists($classname(), "query" _ queryType) {
		set queryBase = $classmethod(, "query" _ queryType, class)
	} elseif 0 {
		/// TODO Some other type of "get stuff between SELECT and FROM"
	} else {
		set sc = $$$ERROR($$$GeneralError, "Query type " _ queryType _ " not found")		
	}
	
	if (queryBase'="") {
		set queryBase = "SELECT " _ queryBase _ " FROM "
	}
	
	return sc
]]></Implementation>
</Method>

<Method name="queryINFO">
<Description><![CDATA[
Return id and dislpayName
<example>w ##class(Form.REST.Objects).queryINFO("Form.Test.Simple")</example>]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>class:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	set method = "getObjectDisplayName"
	
	if ##class(%Dictionary.MethodDefinition).IDKEYExists(class, method) {
		set sqlProc = $$$ClassSQLTable(class) _ "_" _ method _ "(id)"
	} else {
		set sqlProc = $parameter(class, $$$displayProp)
	}
	
	set queryBase = "id AS _id, " _ sqlProc _ " As displayName"
	return queryBase
]]></Implementation>
</Method>

<Method name="queryINFOCLASS">
<Description><![CDATA[
Return id, dislpayName and class
<example>w ##class(Form.REST.Objects).queryINFOCLASS("Form.Test.Simple")</example>]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>class:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	set queryBase = ..queryINFO(class) _ ", %CLASSNAME AS _class "
	return queryBase
]]></Implementation>
</Method>

<Method name="getFormObjectsCustom">
<Description>
Get form objects by a custom query</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>class:%String="",queryType:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#dim sc As %Status = $$$OK
	set sc = ##class(Form.Info).formExistsStatus(class)
	return:$$$ISERR(sc) sc
	set sc = ..checkObjPermission(class, "R")
	return:$$$ISERR(sc) sc
	
	set queryType = $ZCVT(queryType, "U")
		
	if ##class(%Dictionary.MethodDefinition).IDKEYExists(class, "customquery" _ queryType) {
		set sql = $classmethod(class, "customquery" _ queryType)
	} else {
		set sc = $$$ERROR($$$GeneralError, "Custom query type " _ queryType _ " not found")
	}
	
	return:$$$ISERR(sc) sc	
	
	set sc = ##class(Form.JSON.SQL).WriteJSONFromSQL(sql)
	return sc
]]></Implementation>
</Method>
</Class>
</Export>
